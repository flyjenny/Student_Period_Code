let

type arr1 = array of int
type arr2 = array of arr1

var make := arr2 [3] of arr1[3] of 0
var color := arr1[10] of 0
var s := 0
var be := 0
var count :=0

function search(x:int, y:int, z:int) = (
    /*printi(x);print("   ");printi(y);print("\n");*/
    if y <> 0 | x = 0 | make[x-1][0] + make[x-1][1] + make[x-1][2] =15 then (
        if x = 2 & y = 2 then (
            make[2][2]:=45-z;
            s:=make[0][0] + make[0][1] + make[0][2];
            if  make[1][0] + make[1][1] + make[1][2] = s &
                    make[2][0] + make[2][1] + make[2][2] = s &
                    make[0][0] + make[1][0] + make[2][0] = s &
                    make[0][1] + make[1][1] + make[2][1] = s &
                    make[0][2] + make[1][2] + make[2][2] = s &
                    make[0][0] + make[1][1] + make[2][2] = s &
                    make[2][0] + make[1][1] + make[0][2] = s then (
                count :=count+1;
                for i:=0 to 2 do (
                    for j:=0 to 2 do (
                        printi(make[i][j]);
                        print(" ")
                        );
                    print("\n")
                );
                print("\n")
            )
        )
        else (
            if y = 2 then (
                make[x][y] := 15 - make[x][0] - make[x][1];
                if make[x][y]<10 & color[make[x][y]] = 0 then (
                    color[make[x][y]] := 1;
                    if ( y = 2 ) then
                        search(x + 1, 0, z+make[x][y])
                    else
                        search(x, y+1, z+make[x][y]);
                    color[make[x][y]] := 0
                )
            )
            else (
                for i:=1 to 9 do (
                    if color[i] = 0  then (
                        color[i] := 1;
                        make[x][y] := i;
                        if ( y = 2 ) then
                            search(x + 1, 0, z+i)
                        else
                            search(x, y+1, z+i);
                        make[x][y] := 0;
                        color[i] := 0;
                        print("")
                    )
                )
            )
        )
    )
)

in
    search(0,0, 0);
    printi(count)
end 
